<?xml version="1.0" encoding="utf-8"?>

<!--

BigBlueButton open source conferencing system - http://www.bigbluebutton.org/

Copyright (c) 2012 BigBlueButton Inc. and by respective authors (see below).

This program is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License as published by the Free Software
Foundation; either version 3.0 of the License, or (at your option) any later
version.

BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License along
with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.

-->

<mx:ApplicationControlBar xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:mate="http://mate.asfusion.com/" xmlns:common="org.bigbluebutton.common.*"
	xmlns:views="org.bigbluebutton.main.views.*"
	enabled="true" visible="{showToolbar}"
	initialize="init()">   
	
	<mate:Listener type="{ToolbarButtonEvent.ADD}" method="handleAddToolbarButtonEvent" />	
	<mate:Listener type="{ToolbarButtonEvent.REMOVE}" method="handleRemoveToolbarButtonEvent"/>
	<mate:Listener type="{BBBEvent.END_MEETING_EVENT}" method="handleEndMeetingEvent"/>
	<mate:Listener type="{ConnectionFailedEvent.USER_LOGGED_OUT}" method="hideToolbar" />
	<mate:Listener type="{ConnectionFailedEvent.CONNECTION_CLOSED}" method="hideToolbar" />
	<mate:Listener type="{ConfigLoadedEvent.CONFIG_LOADED_EVENT}" method="handleConfigLoadedEvent" />
	<mate:Listener type="{SettingsEvent.SETTINGS_MODULE_LOADED}" method="showSettingsButton" />
	<mate:Listener type="{ShortcutEvent.REMOTE_OPEN_SHORTCUT_WIN}" method="remoteShortcutClick" />
	<mate:Listener type="{ShortcutEvent.LOGOUT}" method="remoteLogout" />
	<mate:Listener type="{ShortcutEvent.FOCUS_SHORTCUT_BUTTON}" method="focusShortcutButton" />
	<mate:Listener type="{ShortcutEvent.FOCUS_LOGOUT_BUTTON}" method="focusLogoutButton" />
	<mate:Listener type="{ConferenceCreatedEvent.CONFERENCE_CREATED_EVENT}" method="retrieveMeetingName" />
	<mate:Listener type="{ConnectionFailedEvent.MODERATOR_DENIED_ME}" method="hideToolbar" />
	<mate:Listener type="{SettingsComponentEvent.ADD}" method="addSettingsComponent" />
	<mate:Listener type="{SettingsComponentEvent.REMOVE}" method="removeSettingsComponent"/>
	<mate:Listener type="{BBBEvent.CHANGE_RECORDING_STATUS}" method="onRecordingStatusChanged" />
	<mate:Listener type="{SuccessfulLoginEvent.USER_LOGGED_IN}" method="refreshModeratorButtonsVisibility" />
	<mate:Listener type="{ChangeMyRole.CHANGE_MY_ROLE_EVENT}" method="refreshRole" />
	<mate:Listener type="{BBBEvent.CONFIRM_LOGOUT_END_MEETING_EVENT}" method="confirmEndSession" />
  
	<mx:Script>
		<![CDATA[
			import com.asfusion.mate.events.Dispatcher;
			
			import mx.binding.utils.BindingUtils;
			import mx.controls.Alert;
			import mx.core.IToolTip;
			import mx.core.UIComponent;
			import mx.events.CloseEvent;
			
			import mx.events.ToolTipEvent;
			import mx.managers.PopUpManager;
			import mx.core.FlexGlobals;

			import org.as3commons.logging.api.ILogger;
			import org.as3commons.logging.api.getClassLogger;
			import org.bigbluebutton.common.IBbbToolbarComponent;
			import org.bigbluebutton.common.events.SettingsComponentEvent;
			import org.bigbluebutton.common.events.ToolbarButtonEvent;
			import org.bigbluebutton.core.BBB;
			import org.bigbluebutton.core.UsersUtil;
			import org.bigbluebutton.core.managers.UserManager;
			import org.bigbluebutton.core.model.Config;
			import org.bigbluebutton.main.events.BBBEvent;
			import org.bigbluebutton.main.events.ConfigLoadedEvent;
			import org.bigbluebutton.main.events.LogoutEvent;
			import org.bigbluebutton.main.events.NetworkStatsEvent;
			import org.bigbluebutton.main.events.SettingsEvent;
			import org.bigbluebutton.main.events.ShortcutEvent;
			import org.bigbluebutton.main.events.SuccessfulLoginEvent;
			import org.bigbluebutton.main.model.LayoutOptions;
			import org.bigbluebutton.main.model.NetworkStatsData;
			import org.bigbluebutton.main.model.users.events.ChangeMyRole;
			import org.bigbluebutton.main.model.users.events.ConferenceCreatedEvent;
			import org.bigbluebutton.main.model.users.events.ConnectionFailedEvent;
			import org.bigbluebutton.util.i18n.ResourceUtil;

			private static const LOGGER:ILogger = getClassLogger(MainToolbar);

			private var DEFAULT_HELP_URL:String = "http://www.bigbluebutton.org/content/videos";
			
			public static const ALIGN_RIGHT:String ="ALIGN_RIGHT";
			public static const ALIGN_LEFT:String = "ALIGN_LEFT";
			
			[Bindable] private var showHelpBtn:Boolean = false;
			[Bindable] private var showToolbar:Boolean = false;
			[Bindable] private var showGuestSettingsButton:Boolean = false;
			[Bindable] private var showRecordButton:Boolean = false;
			[Bindable] public var toolbarOptions:LayoutOptions = new LayoutOptions();		
			[Bindable] private var numButtons:int;
      
			[Bindable] private var _bandwidthConsumedUp:String = "-";
			[Bindable] private var _bandwidthConsumedDown:String = "-";
			private var _updateBandwidthTimer:Timer = new Timer(1000);
			private var _bandwidthToolTip:IToolTip;

			/*
			 * Because of the de-centralized way buttons are added to the toolbar, there is a large gap between the tab indexes of the main buttons
			 * on the left and the tab indexes of the "other" items on the right (shortcut glossary, language slector, etc). This will make it more
			 * convenient to add future items to the tab order.
			 * 
			 * - Justin Robinson, November 13 2012 
			 */
			
			private var xml:XML;
			private var settingsComponents:Array = new Array();
			private var settingsPopup:BBBSettings = null;
			
			private function init():void{
				numButtons = 0;
                
                // Accessibility isn't active till a few second after the client starts to load so we need a delay
                var timer:Timer = new Timer(3000, 1);
                timer.addEventListener(TimerEvent.TIMER, checkAccessiblity);
                timer.start();

				BindingUtils.bindSetter(refreshModeratorButtonsVisibility, UserManager.getInstance().getConference(), "record");
			}
      
      private function checkAccessiblity(e:TimerEvent):void {
        // remove the quick links if there's no screen reader active
        if (!Accessibility.active) {
          quickLinks.removeAllChildren();
        }
      }
      
      private function onQuickLinkClicked(window:String):void {
        var dispatcher:Dispatcher = new Dispatcher();
        switch(window) {
          case "users":
            dispatcher.dispatchEvent(new ShortcutEvent(ShortcutEvent.FOCUS_USERS_WINDOW));
            break;
          case "webcams":
            dispatcher.dispatchEvent(new ShortcutEvent(ShortcutEvent.FOCUS_VIDEO_WINDOW));
            break;
          case "presentation":
            dispatcher.dispatchEvent(new ShortcutEvent(ShortcutEvent.FOCUS_PRESENTATION_WINDOW));
            break;
          case "chat":
            dispatcher.dispatchEvent(new ShortcutEvent(ShortcutEvent.FOCUS_CHAT_WINDOW));
            break;
		  case "caption":
			  dispatcher.dispatchEvent(new ShortcutEvent(ShortcutEvent.FOCUS_CAPTION_WINDOW));
			  break;
         }
       }
								
			public function displayToolbar():void{
				toolbarOptions = new LayoutOptions();
				toolbarOptions.parseOptions();	
				if (toolbarOptions.showToolbar) {
					showToolbar = true;
				} else {
					showToolbar = false;
				}
				
				if (toolbarOptions.showHelpButton) {
					showHelpBtn = true;
				} else {
					showHelpBtn = false;
				}
				if(toolbarOptions.showNetworkMonitor) {
					initBandwidthToolTip();
				}
			}
			
      private function retrieveMeetingName(e:ConferenceCreatedEvent):void {
         if (toolbarOptions.showMeetingName) {
              var meetingTitle:String = BBB.initUserConfigManager().getMeetingTitle();                   
              if (meetingTitle != null) {
                  meetingNameLbl.text = meetingTitle;
                  meetingNameLbl.toolTip = meetingTitle;
               } 
          }
          logFlashPlayerCapabilities();
      }

      private function refreshModeratorButtonsVisibility(e:*):void {
          var userLoaded:Boolean = UserManager.getInstance().getConference().getMyUser() != null
                  && ! UserManager.getInstance().getConference().getMyUser().waitingForAcceptance;

          showGuestSettingsButton = userLoaded && UsersUtil.amIModerator();

          showRecordButton = userLoaded;
      }

      private function getFlashPlayerCapabilities():Object {
          var caps:Object = new Object();
          caps.avHardwareDisable = Capabilities.avHardwareDisable;
          caps.hasAccessibility = Capabilities.hasAccessibility;
          caps.hasAudio = Capabilities.hasAudio;
          caps.hasAudioEncoder = Capabilities.hasAudioEncoder;
          caps.hasEmbeddedVideo = Capabilities.hasEmbeddedVideo;
          caps.hasIME = Capabilities.hasIME;
          caps.hasMP3 = Capabilities.hasMP3;
          caps.hasPrinting = Capabilities.hasPrinting;
          caps.hasScreenBroadcast = Capabilities.hasScreenBroadcast;
          caps.hasScreenPlayback = Capabilities.hasScreenPlayback;
          caps.hasStreamingAudio = Capabilities.hasStreamingAudio;
          caps.hasStreamingVideo = Capabilities.hasStreamingVideo;
          caps.hasTLS = Capabilities.hasTLS;
          caps.hasVideoEncoder = Capabilities.hasVideoEncoder;
          caps.isDebugger = Capabilities.isDebugger;
          caps.isEmbeddedInAcrobat = Capabilities.isEmbeddedInAcrobat;
          caps.language = Capabilities.language;
          caps.localFileReadDisable = Capabilities.localFileReadDisable;
          caps.manufacturer = Capabilities.manufacturer;
          caps.maxLevelIDC = Capabilities.maxLevelIDC;
          caps.os = Capabilities.os;
          caps.pixelAspectRatio = Capabilities.pixelAspectRatio;
          caps.playerType = Capabilities.playerType;
          caps.screenColor = Capabilities.screenColor;
          caps.screenDPI = Capabilities.screenDPI;
          caps.screenResolutionX = Capabilities.screenResolutionX;
          caps.screenResolutionY = Capabilities.screenResolutionY;
          //caps.serverString = Capabilities.serverString;
          caps.touchscreenType = Capabilities.touchscreenType;
          caps.version = Capabilities.version;
          return caps;
     }

     private function logFlashPlayerCapabilities():void {
         var logData:Object = UsersUtil.initLogData();
         logData.tags = ["initialization"];
         logData.capabilities = getFlashPlayerCapabilities();
         logData.message = "Flash Player Capabilities.";
         LOGGER.info(JSON.stringify(logData));
     }

            
			public function addButton(name:String):Button{
				var btn:Button = new Button();
				btn.id = name;
				btn.label = name;
				btn.height = 20;
				btn.visible = true;
				this.addChild(btn);
				
				return btn;
			}
			
			private function onHelpButtonClicked():void {
				navigateToURL(new URLRequest(DEFAULT_HELP_URL))
			}
			
			private function handleEndMeetingEvent(event:BBBEvent):void {
				LOGGER.debug("Received end meeting event.");
				doLogout();
			}
			
			
			private function confirmLogout():void {
				if (toolbarOptions.confirmLogout) {
					var logoutWindow:LogoutWindow;
					logoutWindow = LogoutWindow(PopUpManager.createPopUp(FlexGlobals.topLevelApplication as DisplayObject, LogoutWindow, true));

					var newX:Number = btnLogout.x + btnLogout.width - logoutWindow.width;
					var newY:Number = btnLogout.y + btnLogout.height + 5;

					PopUpManager.centerPopUp(logoutWindow);

					logoutWindow.x = newX;
					logoutWindow.y = newY;
				} else {
					doLogout();
				}
			}
			
						
			private function alertLogout(e:CloseEvent):void {
				// Check to see if the YES button was pressed.
				if (e.detail==Alert.YES) {
					UserManager.getInstance().getConference().setIAskedToLogout(true);
					/* 
					 * If doLogout() is called immediately there is a null exception in AlertAccImpl
					 * line 185, but if we delay calling doLogout() until the next frame the Alert 
					 * will close correctly and the logout succeeds as normal. It looks like the 
					 * stage is unset at some point in the core class and there's not much we can do
					 * to correct it other than avoid it. - Chad Aug 8, 2016 
					 */
					callLater(doLogout);
				}
			}
			
			private function doLogout():void {
				dispatchEvent(new LogoutEvent(LogoutEvent.USER_LOGGED_OUT));
			}

			private function confirmEndSession(event:BBBEvent):void {
				var alert:Alert = Alert.show(ResourceUtil.getInstance().getString('bbb.endSession.confirm.message'), ResourceUtil.getInstance().getString('bbb.endSession.confirm.title'), Alert.YES | Alert.NO, null, onConfirmEndSessionAlertClosed, null, Alert.YES);
				// we need to set transparency duration to avoid the blur effect when two alerts are displayed sequentially
				alert.setStyle("modalTransparencyDuration", 250);
			}

			private function onConfirmEndSessionAlertClosed(e:CloseEvent):void {
				// Check to see if the YES button was pressed.
				if (e.detail == Alert.YES) {
					endSession();
				}
			}

			private function endSession():void {
				dispatchEvent(new BBBEvent(BBBEvent.LOGOUT_END_MEETING_EVENT));
			}
			
			private function hideToolbar(e:ConnectionFailedEvent):void{
				if (toolbarOptions.showToolbar) {
					this.visible = false;
				} else {
					this.visible = true;
				}
			}
			
			private function handleAddToolbarButtonEvent(event:ToolbarButtonEvent):void {
				// Find out how to import accessibility into custom components; even though the ToolbarButtons are buttons, they don't seem to have a tabIndex
				if (event.location == ToolbarButtonEvent.TOP_TOOLBAR) {
					var index:int;
					var added:int = 0;
					//*var localButton:UIComponent;
					if (event.module == "DeskShare"){
						index = 0;
					}
					else if (event.module == "Microphone"){
						index = 1;
					}
					else if (event.module == "Webcam"){
						index = 2;
					}
					
					//localButton = event.button as UIComponent;
					//(localButton as Button).tabIndex = baseIndex + numButtons;
					//addedBtns.addChild(localButton);*/
					
					
					addedBtns.addChild(event.button as UIComponent);
					// Won't work quite like this: addedBtns.addChildAt(event.button as UIComponent, index);					
					numButtons++;
					realignButtons();
				}
			}
			
			private function handleRemoveToolbarButtonEvent(event:ToolbarButtonEvent):void {
				if (addedBtns.contains(event.button as UIComponent))
					addedBtns.removeChild(event.button as UIComponent);
			}
			
			private function realignButtons():void{
				for each (var button:UIComponent in addedBtns.getChildren()){
					var toolbarComponent:IBbbToolbarComponent = button as IBbbToolbarComponent;
					
					/*if (toolbarComponent.getAlignment() == ALIGN_LEFT){ 
						addedBtns.setChildIndex(button, 0);
						//(addedBtns.getChildAt(0) as Button).tabIndex = 0;
					}
					else if (toolbarComponent.getAlignment() == ALIGN_RIGHT){ 
						addedBtns.setChildIndex(button, addedBtns.numChildren - 1);
						//(addedBtns.getChildAt(0) as Button).tabIndex = addedBtns.numChildren - 1;
					}*/
					button.tabIndex = quickLinksIndexer.startIndex + 5;
					//for (var i:int = 0; i < addedBtns.numChildren; i++){
					//	(addedBtns.getChildAt(i) as Button).tabIndex = baseIndex + i;
					//}
				}
			}
						
			private function handleConfigLoadedEvent(e:ConfigLoadedEvent):void{
				var config:Config = BBB.getConfigManager().config;
				shortcutKeysBtn.includeInLayout = shortcutKeysBtn.visible = config.shortcutKeys.showButton;

				if (config.branding.logo == "") {
					hideLogo();
				} else {
					logo.source = config.branding.logo;
				}

				if (config.branding.toolbarColor != "") {
					setStyle("backgroundColor", config.branding.toolbarColor);
				}

				if (config.branding.toolbarColorAlphas != "") {
					setStyle("highlightAlphas", config.branding.toolbarColorAlphas.split(","));
				}
			}
			
			private function onDisconnectTest():void{
				var d:Dispatcher = new Dispatcher();
				var e:LogoutEvent = new LogoutEvent(LogoutEvent.DISCONNECT_TEST);
				d.dispatchEvent(e);
			}
			
			private function showSettingsButton(e:SettingsEvent):void{
				var b:Button = new Button();
				b.label = ResourceUtil.getInstance().getString('bbb.mainToolbar.settingsBtn');
				b.toolTip = ResourceUtil.getInstance().getString('bbb.mainToolbar.settingsBtn.toolTip');
				b.addEventListener(MouseEvent.CLICK, openSettings);
				this.addChild(b);
			}
			
			private function openSettings(e:Event = null):void{
				var d:Dispatcher = new Dispatcher();
				d.dispatchEvent(new SettingsEvent(SettingsEvent.OPEN_SETTINGS_PANEL));
			}
			
			public function remoteShortcutClick(e:ShortcutEvent):void{
				onShortcutButtonClick();
			}
			
			public function remoteLogout(e:ShortcutEvent):void{
				confirmLogout();
			}
			
			private function onShortcutButtonClick(e:Event = null):void {
				var d:Dispatcher = new Dispatcher();
				d.dispatchEvent(new ShortcutEvent(ShortcutEvent.OPEN_SHORTCUT_WIN));
			}
			
			private function focusShortcutButton(e:ShortcutEvent):void{
				shortcutKeysBtn.setFocus();
			}
			
			private function focusLogoutButton(e:ShortcutEvent):void{
				btnLogout.setFocus();
			}
      
      private function onRecordingStatusChanged(event:BBBEvent):void {
        meetingNameLbl.text = "";
        
        if (event.payload.recording) {
          meetingNameLbl.text =  ResourceUtil.getInstance().getString('bbb.mainToolbar.recordingLabel.recording')
		}
        
        if (toolbarOptions.showMeetingName) {
          var meetingTitle:String = BBB.initUserConfigManager().getMeetingTitle();                   
          if (meetingTitle != null) {
            meetingNameLbl.text += " " + meetingTitle;
          } 
        }
      }

			private function onNetStatsButtonClick(e:Event = null):void {
				var d:Dispatcher = new Dispatcher();
				d.dispatchEvent(new NetworkStatsEvent(NetworkStatsEvent.OPEN_NETSTATS_WIN));
			}

			private function initBandwidthToolTip():void {
				_updateBandwidthTimer.addEventListener(TimerEvent.TIMER, updateBandwidthTimerHandler);
				_updateBandwidthTimer.start();
				btnNetwork.addEventListener(ToolTipEvent.TOOL_TIP_SHOW, bwToolTipShowHandler);
				btnNetwork.addEventListener(ToolTipEvent.TOOL_TIP_END, bwToolTipEndHandler);
			}

			private function bwToolTipShowHandler(e:ToolTipEvent):void {
				// The ToolTip must be stored so it's text can be updated
				_bandwidthToolTip = e.toolTip;
				updateBwToolTip();
			}

			private function bwToolTipEndHandler(e:ToolTipEvent):void {
				_bandwidthToolTip = null;
			}

			private function updateBandwidthTimerHandler(e:TimerEvent):void {
				_bandwidthConsumedDown = NetworkStatsData.getInstance().formattedCurrentConsumedDownBW;
				_bandwidthConsumedUp = NetworkStatsData.getInstance().formattedCurrentConsumedUpBW;
				updateBwToolTip();
			}

			private function updateBwToolTip():void {
				if(_bandwidthToolTip) {
					_bandwidthToolTip.text = ResourceUtil.getInstance().getString('bbb.bwmonitor.upload.short') + ": " + _bandwidthConsumedUp +
						" | " + ResourceUtil.getInstance().getString('bbb.bwmonitor.download.short')+": "+_bandwidthConsumedDown;
				}
			}

			private function hideLogo():void {
				logoHolder.visible = logoHolder.includeInLayout = false;
			}

			private function addSettingsComponent(e:SettingsComponentEvent = null):void {
				settingsComponents.push(e.component);
			}

			private function removeSettingsComponent(e:SettingsComponentEvent = null):void {
				throw("Not implemented");
			}

			private function onSettingsButtonClick():void {
				settingsPopup = BBBSettings(PopUpManager.createPopUp(this.parent, BBBSettings, true));
				settingsPopup.pushComponents(settingsComponents);
			}

			private function refreshRole(e:ChangeMyRole):void {
				refreshModeratorButtonsVisibility(null);
			}
		]]>
	</mx:Script>
	
	<common:TabIndexer id="quickLinksIndexer" startIndex="102" tabIndices="{[usersLinkBtn, webcamLinkButton, presentationLinkBtn, chatLinkBtn, captionLinkBtn]}"/>
	<common:TabIndexer id="buttonsIndexer" startIndex="{quickLinksIndexer.startIndex + numButtons + 10}"
					   tabIndices="{[recordBtn, muteMeBtn, shortcutKeysBtn, helpBtn, btnLogout]}"/>

    
    <mx:HBox id="logoHolder" paddingRight="12" >
        <mx:Image id="logo" ioError="hideLogo()" />
    </mx:HBox>
    <mx:HBox id="quickLinks" width="1" includeInLayout="false">
        <mx:LinkButton id="usersLinkBtn" click="onQuickLinkClicked('users')" label="{ResourceUtil.getInstance().getString('bbb.users.quickLink.label')}"
                       accessibilityDescription="{usersLinkBtn.label}" toolTip="{usersLinkBtn.label}"
                       height="22" styleName="quickWindowLinkStyle" />
        <mx:LinkButton id="webcamLinkButton" click="onQuickLinkClicked('webcams')" label="{ResourceUtil.getInstance().getString('bbb.videodock.quickLink.label')}" 
                       accessibilityDescription="{webcamLinkButton.label}" toolTip="{webcamLinkButton.label}"
                       height="22" styleName="quickWindowLinkStyle" />
        <mx:LinkButton id="presentationLinkBtn" click="onQuickLinkClicked('presentation')" label="{ResourceUtil.getInstance().getString('bbb.presentation.quickLink.label')}"
                       accessibilityDescription="{presentationLinkBtn.label}" toolTip="{presentationLinkBtn.label}"
                       height="22" styleName="quickWindowLinkStyle" />
        <mx:LinkButton id="chatLinkBtn" click="onQuickLinkClicked('chat')" label="{ResourceUtil.getInstance().getString('bbb.chat.quickLink.label')}" 
                       accessibilityDescription="{chatLinkBtn.label}" toolTip="{chatLinkBtn.label}"
                       height="22" styleName="quickWindowLinkStyle" />
		<mx:LinkButton id="captionLinkBtn" click="onQuickLinkClicked('caption')" label="{ResourceUtil.getInstance().getString('bbb.caption.quickLink.label')}" 
					   accessibilityDescription="{captionLinkBtn.label}" toolTip="{captionLinkBtn.label}"
					   height="22" styleName="quickWindowLinkStyle" />
    </mx:HBox>
	<mx:HBox id="addedBtns"/>
	<views:RecordButton id="recordBtn" visible="{showRecordButton}" includeInLayout="{showRecordButton}"/>
	<mx:VRule strokeWidth="2" height="100%" visible="{muteMeBtn.visible}" includeInLayout="{muteMeBtn.includeInLayout}"/>
	<views:MuteMeButton id="muteMeBtn"  height="20"/>
	<mx:Label id="meetingNameLbl" width="100%" minWidth="1" styleName="meetingNameLabelStyle" />

	<mx:Button
			id="bbbSettings"
			visible="{showGuestSettingsButton}"
			includeInLayout="{showGuestSettingsButton}"
			toolTip="{ResourceUtil.getInstance().getString('bbb.settings.btn.toolTip')}"
			click="onSettingsButtonClick()"
			styleName="settingsButtonStyle"
			height="22"
	/>
<!--
  <mx:Button label="DISCONNECT!" click="BBB.initConnectionManager().forceClose()" height="22" toolTip="Click to simulate disconnection" />
-->
	<mx:Button
			id="btnNetwork"
			styleName="bandwidthButtonStyle"
			toolTip="dummy text"
			click="onNetStatsButtonClick()"
			visible="{toolbarOptions.showNetworkMonitor}" />
	<mx:Button id="shortcutKeysBtn" label="{ResourceUtil.getInstance().getString('bbb.mainToolbar.shortcutBtn')}" styleName="shortcutButtonStyle"
             click="onShortcutButtonClick()" height="22" 
             toolTip="{ResourceUtil.getInstance().getString('bbb.mainToolbar.shortcutBtn.toolTip')}"/>
	<mx:LinkButton id="helpBtn" visible="{showHelpBtn}" includeInLayout="{showHelpBtn}" label="?" click="onHelpButtonClicked()" height="22" 
                   styleName="helpLinkButtonStyle"
                   toolTip="{ResourceUtil.getInstance().getString('bbb.mainToolbar.helpBtn')}"
				   accessibilityName="{ResourceUtil.getInstance().getString('bbb.micSettings.access.helpButton')}"/>
	<mx:Button label="" id="btnLogout" styleName="logoutButtonStyle"
                 toolTip="{ResourceUtil.getInstance().getString('bbb.mainToolbar.logoutBtn.toolTip')}" right="10" click="confirmLogout()" height="22"/>
</mx:ApplicationControlBar>
